import csv

include: "rules/hmftools.smk"
include: "rules/demo.smk"

configfile: srcdir("config.yaml")

genotyping_window_size = 10
phasing_window_size = 100
calling_window_size = 10

def get_calling_windows(win_size_mb):
    MB=1000000
    window_size = win_size_mb * MB
    fai = config["reference"] + ".fai"
    chromosomes = [ "chr" + str(i) for i in range(0, 23) ]
    chromosomes.append("chrX")
    chromosomes.append("chrY")
    windows = list()
    with open(fai) as f:
        for line in f:
            fields = line.rstrip().split()
            chrom_name = fields[0]
            chrom_len = int(fields[1])

            if chrom_name not in chromosomes:
                continue

            for i in range(1, chrom_len, window_size):
                end = min(chrom_len, i + window_size)
                windows.append( f"{chrom_name}:{i}-{end}" )
    return windows

#
# Downloaded GRCh38 resources
#
rule get_gnomad_af_vcf_gz:
    output:
        "resources/af-only-gnomad.hg38.vcf.gz"
    params:
        memory_per_thread="4G",
        extra_cluster_opt=""
    shell:
        "wget -O {output} https://storage.googleapis.com/gatk-best-practices/somatic-hg38/af-only-gnomad.hg38.vcf.gz"

rule get_gnomad_af_common:
    input:
        "resources/af-only-gnomad.hg38.vcf.gz"
    output:
        "resources/af-only-gnomad.hg38.p001_biallelic_snps.vcf"
    params:
        memory_per_thread="4G",
        extra_cluster_opt=""
    shell:
        "bcftools filter -i 'INFO/AF > 0.001 & TYPE == \"snp\" & N_ALT == 1' {input} | grep -v random | grep -v alt > {output}"

rule get_giab_hc_bed_gz:
    output:
        "resources/GRCh38_notinalldifficultregions.bed.gz"
    params:
        memory_per_thread="4G",
        extra_cluster_opt=""
    shell:
        "wget -O {output} https://raw.githubusercontent.com/jts/smrest/main/etc/GRCh38_notinalldifficultregions.bed.gz"

rule get_giab_hc_bed:
    input:
        "resources/GRCh38_notinalldifficultregions.bed.gz"
    output:
        "resources/GRCh38_notinalldifficultregions.bed"
    params:
        memory_per_thread="4G",
        extra_cluster_opt=""
    shell:
        "gunzip {input}"

rule prepare_resources:
    input:
        "resources/GRCh38_notinalldifficultregions.bed", "resources/genotype_sites.vcf"

#
# Genotype gnomad SNPs
#
rule get_on_target_gnomad:
    input:
        config["gnomad_common"]
    output:
        "resources/genotype_sites.vcf"
    params:
        memory_per_thread="4G",
        extra_cluster_opt=""
    threads: 1
    shell:
        "bcftools filter -T {config[giab_hc_bed]} {config[gnomad_common]} > {output}"

rule genotype_regions:
    input:
        bam="data/{sample}.bam",
        bai="data/{sample}.bam.bai",
        vcf="resources/genotype_sites.vcf"
    params:
        memory_per_thread="32G",
        extra_cluster_opt=""
    output:
        "smrest_genotype/{sample}/per_region/{sample}.region.{region}.gnomad_genotype.vcf"
    shell:
        "{config[smrest]} genotype-hets -c {input.vcf} -r {wildcards.region} -g {config[reference]} {input.bam} > {output}"

rule merge_region_genotype:
    input:
        vcfs=expand("smrest_genotype/{{sample}}/per_region/{{sample}}.region.{r}.gnomad_genotype.vcf", r=get_calling_windows(genotyping_window_size))
    output:
        "smrest_genotype/{sample}/{sample}.gnomad_genotype.vcf"
    params:
        memory_per_thread="4G",
        extra_cluster_opt=""
    shell:
        "bcftools concat {input.vcfs} | bcftools sort > {output}"

#
# Phase the genotyping results
#

rule compress_vcf:
    input:
        vcf="{prefix}.vcf"
    output:
        vcf="{prefix}.vcf.gz"
    threads: 1
    params:
        memory_per_thread="8G",
        extra_cluster_opt=""
    shell:
        "bgzip {input}"

rule tabix_vcf:
    input:
        vcf="{prefix}.vcf.gz"
    output:
        tbi="{prefix}.vcf.gz.tbi"
    threads: 1
    params:
        memory_per_thread="8G",
        extra_cluster_opt=""
    shell:
        "tabix {input}"

rule get_region_vcf:
    input:
        vcf="smrest_genotype/{sample}/{sample}.gnomad_genotype.vcf.gz",
        tbi="smrest_genotype/{sample}/{sample}.gnomad_genotype.vcf.gz.tbi"
    output:       
        vcf="phasing/{sample}/region_vcf/{sample}.gnomad_genotype.{region}.vcf"
    threads: 1
    params:
        memory_per_thread="8G",
        extra_cluster_opt=""
    shell:
        "bcftools view {input.vcf} {wildcards.region} > {output.vcf}"

rule phase_variants_whatshap:
    input:
        vcf="phasing/{sample}/region_vcf/{sample}.gnomad_genotype.{region}.vcf",
        bam="data/{sample}.bam"
    output:
        vcf="phasing/{sample}/phased_region_vcf/{sample}.gnomad_genotype.{region}.whatshap_phased.vcf"
    threads: 1
    params:
        memory_per_thread="24G",
        extra_cluster_opt=""
    shell:   
        "whatshap phase --ignore-read-groups -r {config[reference]} -o {output.vcf} {input.vcf} {input.bam}"

rule merge_phased_vcfs:
    input:
        vcfs=expand("phasing/{{sample}}/phased_region_vcf/{{sample}}.gnomad_genotype.{r}.whatshap_phased.vcf", r=get_calling_windows(phasing_window_size))
    output:
        "phasing/{sample}/{sample}.gnomad_genotype.whatshap_phased.vcf"
    params:
        memory_per_thread="4G",
        extra_cluster_opt=""
    shell:
        "bcftools concat {input.vcfs} | bcftools sort > {output}"

def get_fai(wildcards):
    return config['reference'] + ".fai"

#
# Somatic mutation calling
#
rule call_regions_internal_tag:
    input:
        #bam="haplotag/{sample}/{sample}.gnomad_genotype.whatshap_phased.tagged.bam",
        #bai="haplotag/{sample}/{sample}.gnomad_genotype.whatshap_phased.tagged.bam.bai"
        bam="data/{sample}.bam",
        bai="data/{sample}.bam.bai",
        phased_vcf="phasing/{sample}/{sample}.gnomad_genotype.whatshap_phased.vcf"
    params:
        memory_per_thread="24G",
        extra_cluster_opt=""
    output:
        vcf="smrest_calls/{sample}/per_region/{sample}.whatshap_phased.region.{region}.vcf",
        bed="smrest_calls/{sample}/per_region/{sample}.whatshap_phased.region.{region}.bed"
    shell:
        "{config[smrest]} call -m haplotype-likelihood --purity 0.5 -r {wildcards.region} -g {config[reference]} -p {input.phased_vcf} -o {output.bed} {input.bam} > {output.vcf}"

rule merge_region_calls:
    input:
        vcfs=expand("smrest_calls/{{sample}}/per_region/{{sample}}.whatshap_phased.region.{r}.vcf", r=get_calling_windows(calling_window_size))
    output:
        "smrest_calls/{sample}/{sample}.{phaser}.calls.vcf"
    params:
        memory_per_thread="4G",
        extra_cluster_opt=""
    shell:
        "bcftools concat {input.vcfs} | bcftools sort > {output}"

rule merge_region_bed:
    input:
        beds=expand("smrest_calls/{{sample}}/per_region/{{sample}}.whatshap_phased.region.{r}.bed", r=get_calling_windows(calling_window_size))
    output:
        "smrest_calls/{sample}/{sample}.{phaser}.called_regions.bed"
    params:
        memory_per_thread="4G",
        extra_cluster_opt=""
    shell:
        "cat {input.beds} > {output}"

rule final_bed:
    input:
        "smrest_calls/{sample}/{sample}.{phaser}.called_regions.bed"
    output:
        "smrest_calls/{sample}/{sample}.{phaser}.final_call_regions.bed"
    params:
        memory_per_thread="4G",
        extra_cluster_opt=""
    shell:
        """bedtools intersect -b {config[giab_hc_bed]} \
                              -a {input} > {output}"""

rule final_calls:
    input:
        vcf="smrest_calls/{sample}/{sample}.{phaser}.calls.vcf.gz",
        tbi="smrest_calls/{sample}/{sample}.{phaser}.calls.vcf.gz.tbi",
        bed="smrest_calls/{sample}/{sample}.{phaser}.final_call_regions.bed"
    output:
        "smrest_calls/{sample}/{sample}.{phaser}.final_q{min_somatic_qual}_pass_calls.vcf"
    params:
        memory_per_thread="4G",
        extra_cluster_opt=""
    shell:
        """
        if [ -s {input.bed} ]; then
            bcftools filter -T {input.bed} -i 'QUAL > {wildcards.min_somatic_qual} && (FILTER == \"PASS\" || FILTER == \".\")' {input.vcf} > {output}
        else
            zgrep \"^#\" {input.vcf} > {output}
        fi
        """

rule index_bam:
    input:
        "{prefix}.bam"
    output:
        "{prefix}.bam.bai"
    threads: 1
    params:
        memory_per_thread="4G",
        extra_cluster_opt=""
    shell:
        "samtools index {input}"
